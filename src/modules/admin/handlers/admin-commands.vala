using BarsuTimetable;
using DataStore;
using Telegram;
using Gee;

namespace Admin {
    
    public class AdminCommands {
        
        public async void stat(Message msg) {
            if (msg.get_command_arguments() == "teacher") {
                string text = "–ü—Ä–µ–ø–æ–¥–∞–≤–∞—Ç–µ–ª–∏:\n";
                foreach (var config in data.get_users()) {
                    if (config.post != UserPost.TEACHER)
                        continue;
                    
                    text += @"[$(config.name)](tg://user?id=$(config.id))\n";
                }
                
                yield bot.send(new SendMessage() {
                    chat_id = msg.chat.id,
                    parse_mode = ParseMode.MARKDOWN,
                    text = text
                });
                
                return;
            }
            
            int sub_count = 0, registered = 0, in_setup = 0, teachers = 0;
            
            HashMap<string, int> chats = new HashMap<string, int>();
            foreach (var config in data.get_chats()) {
                if (chats.has_key(config.group))
                    chats.set(config.group, chats.get(config.group) + 1);
                else
                    chats.set(config.group, 1);
            }
            
            foreach (var config in data.get_users()) {
                if (config.subscribed)
                    sub_count++;
                
                if (config.state != null)
                    in_setup++;
                
                if (config.post != null)
                    registered++;
                
                if (config.post == UserPost.TEACHER)
                    teachers++;
            }
            
            int count = 0;
            string text = "üë•Ô∏è –ì—Ä—É–ø–ø—ã:\n";
            foreach (var chat in chats) {
                text += @"$(chat.key) - $(chat.value)\n";
                count += chat.value;
            }
            text += @"–í—Å–µ–≥–æ: *$count*\n";
            
            text += "\nüë§Ô∏è –ü–æ–ª—å–∑–æ–≤–∞—Ç–µ–ª–∏:\n";
            text += @"–í—Å–µ–≥–æ: $(data.get_users().size) (*$registered*/$in_setup)\n";
            text += @"–ü—Ä–µ–ø–æ–¥–∞–≤–∞—Ç–µ–ª–∏: *$teachers*\n";
            text += @"–ü–æ–¥–ø–∏—Å–∞–Ω–æ: $sub_count";
            
            yield bot.send(new SendMessage() {
                parse_mode = ParseMode.MARKDOWN,
                chat_id = msg.chat.id,
                text = text
            });
        }
        
        public async void broadcast(Message msg) {
            if (msg.reply_to_message == null) {
                yield bot.send(new SendMessage() {
                    chat_id = msg.chat.id,
                    text = "‚ö†Ô∏è –ù—É–∂–Ω–æ –æ—Ç–ø—Ä–∞–≤–ª—è—Ç—å –æ—Ç–≤–µ—Ç–æ–º –Ω–∞ —Å–æ–æ–±—â–µ–Ω–∏–µ"
                });
                
                return;
            }
            
            yield broadcast_manager.send_broadcast(msg.chat.id, msg.reply_to_message.message_id, false);
        }
        
        public async void sync(Message msg) {
            var last_fetch = group_manager.get_last_fetch();
            var response = yield bot.send(new SendMessage() {
                chat_id = msg.chat.id,
                parse_mode = ParseMode.MARKDOWN,
                text = "*–ì—Ä—É–ø–ø—ã —Å–∏–Ω—Ö—Ä–æ–Ω–∏–∑–∏—Ä—É—é—Ç—Å—è...*\n\n" +
                       @"–ü–æ—Å–ª–µ–¥–Ω–µ–µ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ: $last_fetch"
            });
            var message_id = new Message(response.result.get_object()).message_id;
            
            yield group_manager.sync();
            yield bot.send(new EditMessageText() {
                chat_id = msg.chat.id,
                message_id = message_id,
                parse_mode = ParseMode.MARKDOWN,
                text = "*–ì—Ä—É–ø–ø—ã –æ–±–Ω–æ–≤–ª–µ–Ω—ã!*\n\n" +
                      @"–ü–æ—Å–ª–µ–¥–Ω–µ–µ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ: $last_fetch"
            });
        }
        
        public async void update_commands(Message msg) {
            var day = new BotCommand() {
                command = "day",
                description = "–ù–∞ —Å–µ–≥–æ–¥–Ω—è"
            };
            var tomorrow = new BotCommand() {
                command = "tomorrow",
                description = "–ù–∞ –∑–∞–≤—Ç—Ä–∞"
            };
            var rasp = new BotCommand() {
                command = "rasp",
                description = "–ù–∞ —ç—Ç—É –Ω–µ–¥–µ–ª—é"
            };
            var raspnext = new BotCommand() {
                command = "raspnext",
                description = "–ù–∞ —Å–ª–µ–¥. –Ω–µ–¥–µ–ª—é"
            };
            var next = new BotCommand() {
                command = "next",
                description = "–°–ª–µ–¥—É—é—â–∞—è –ø–∞—Ä–∞"
            };
            var bells = new BotCommand() {
                command = "bells",
                description = "–ó–≤–æ–Ω–∫–∏"
            };
            var bus = new BotCommand() {
                command = "bus",
                description = "–ë–ª–∏–∂–∞–π—à–∏–µ –∞–≤—Ç–æ–±—É—Å—ã"
            };
            var settings = new BotCommand() {
                command = "settings",
                description = "–ù–∞—Å—Ç—Ä–æ–π–∫–∏"
            };
            var help = new BotCommand() {
                command = "help",
                description = "–ü–æ–∫–∞–∑–∞—Ç—å –ø–æ–º–æ—â—å"
            };
            
            yield bot.send(new SetMyCommands() {
                commands = { day, tomorrow, rasp, raspnext, next, bells, bus, help },
                scope = new BotCommandScopeDefault()
            });
            yield bot.send(new SetMyCommands() {
                commands = { day, tomorrow, rasp, raspnext, next, bells, bus, settings, help },
                scope = new BotCommandScopeAllPrivateChats()
            });
            yield bot.send(new SetMyCommands() {
                commands = { day, tomorrow, rasp, raspnext, next, bells, bus, settings, help },
                scope = new BotCommandScopeAllChatAdministrators()
            });
        }
    }
}
